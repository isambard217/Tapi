buildscript {
	ext {
		springBootVersion = '2.0.4.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}
plugins {
    id "org.sonarqube" version "2.6.2"
	id "net.ltgt.errorprone" version "0.0.16"
}
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'findbugs'
apply plugin: 'idea'
findbugs {
    sourceSets = [sourceSets.main]
}
group = 'com.ops.base'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
bootJar {
	baseName='the_trace_api'
	version='0.0.1'
}
repositories {
	mavenCentral()
}
dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-web')
	// https://mvnrepository.com/artifact/io.springfox/springfox-swagger2
	compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
	// https://mvnrepository.com/artifact/io.springfox/springfox-swagger-ui
	compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
	// https://mvnrepository.com/artifact/mysql/mysql-connector-java
	compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.12'
	compile("com.h2database:h2")
	// https://mvnrepository.com/artifact/io.cucumber/cucumber-java
	compile group: 'io.cucumber', name: 'cucumber-java', version: '3.0.2'
// https://mvnrepository.com/artifact/io.cucumber/cucumber-junit
	testCompile group: 'io.cucumber', name: 'cucumber-junit', version: '3.0.2'
	compile group: 'com.opencsv', name: 'opencsv', version: '4.1'
    // https://mvnrepository.com/artifact/io.jsonwebtoken/jjwt
    compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.7.0'
	// https://mvnrepository.com/artifact/org.modelmapper/modelmapper
	compile group: 'org.modelmapper', name: 'modelmapper', version: '0.7.5'
    compile 'com.google.guava:guava:26.0-jre'
	compileOnly "org.springframework.boot:spring-boot-configuration-processor"
}
task cucumber(dependsOn: testClasses) {
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.testRuntimeClasspath + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'com.ops.base.education.project', 'src/test/resources']
        }
    }
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}
task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}
tasks.withType(JavaCompile) {
    // In Gradle 4.9+, prefer using tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
            '-Xep:DeadException:ERROR',
            '-Xep:ReferenceEquality:ERROR'
    ]
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}
